# Check if this loader is enabled
if(NOT OPTION_BUILD_LOADERS OR NOT OPTION_BUILD_LOADERS_NODE OR NOT OPTION_BUILD_LOADERS_TS)
	return()
endif()

#
# Plugin name and options
#

# Target name
set(target ts_loader_bootstrap)

# Exit here if required dependencies are not met
message(STATUS "Plugin ${target} bootstrap")

#
# Create target
#

set(source_path "${CMAKE_CURRENT_SOURCE_DIR}/lib")

set(sources
	${source_path}/bootstrap.ts
)

# Group source files
set(source_group "Source Files")
source_group_by_path(${source_path}  "\\\\.ts$"
	${source_group} ${sources})

if(MSVC)
	set(BOOTSTRAP_OUTPUT_PATH "${PROJECT_BINARY_DIR}/${CMAKE_BUILD_TYPE}")
else()
	set(BOOTSTRAP_OUTPUT_PATH "${PROJECT_BINARY_DIR}")
endif()

if(NOT OPTION_BUILD_GUIX)
	# Dependencies
	add_custom_target(${target}_depends
		WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/lib
		COMMAND npm install
		SOURCES ${sources}
	)

	set_target_properties(${target}_depends
		PROPERTIES
		${DEFAULT_PROJECT_OPTIONS}
		FOLDER "${IDE_FOLDER}"
	)

	# Build Bootstrap
	add_custom_target(${target} ALL
		WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/lib
		COMMAND npm run build
		COMMAND ${CMAKE_COMMAND} -E copy_directory ${CMAKE_CURRENT_SOURCE_DIR}/lib/node_modules/typescript ${BOOTSTRAP_OUTPUT_PATH}/node_modules/typescript
		COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_CURRENT_SOURCE_DIR}/lib/build/bootstrap.js ${BOOTSTRAP_OUTPUT_PATH}/bootstrap.ts
		SOURCES ${sources}
	)

	set_target_properties(${target}
		PROPERTIES
		${DEFAULT_PROJECT_OPTIONS}
		FOLDER "${IDE_FOLDER}"
	)

	add_dependencies(${target} ${target}_depends)

	# Test
	add_test(NAME ${target}
		COMMAND npm test
		WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/lib
	)

	set_property(TEST ${target}
		PROPERTY LABELS ${target}
	)

	# Install TypeScript
	install(DIRECTORY
		${CMAKE_CURRENT_SOURCE_DIR}/lib/node_modules/typescript
		DESTINATION ${INSTALL_LIB}/node_modules
		COMPONENT runtime
	)
else()
	set(NODEJS_EXECUTABLE_ONLY ON)

	find_package(NodeJS)

	if(NOT NODEJS_FOUND)
		message(STATUS "NodeJS executable not found, skipping ts loader bootstrap build")
		return()
	endif()

	# Build Bootstrap (without typechecking)
	add_custom_target(${target} ALL
		WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/lib
		COMMAND ${NODEJS_EXECUTABLE} compile.js
		COMMAND ${CMAKE_COMMAND} -E copy ${CMAKE_CURRENT_SOURCE_DIR}/lib/build/bootstrap.js ${BOOTSTRAP_OUTPUT_PATH}/bootstrap.ts
		SOURCES ${sources}
	)

	set_target_properties(${target}
		PROPERTIES
		${DEFAULT_PROJECT_OPTIONS}
		FOLDER "${IDE_FOLDER}"
	)

	# Guix stores typescript dependency previously to the build in the output directory
	install(DIRECTORY
		${BOOTSTRAP_OUTPUT_PATH}/node_modules/typescript
		DESTINATION ${INSTALL_LIB}/node_modules
		COMPONENT runtime
	)
endif()

# Install Bootstrap (use ts extension even if it is transpiled js to avoid conflicts with node loader bootstrap)
install(FILES
	${BOOTSTRAP_OUTPUT_PATH}/bootstrap.ts
	DESTINATION ${INSTALL_LIB}
	COMPONENT runtime
)
