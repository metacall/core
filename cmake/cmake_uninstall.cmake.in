
#
# CMake template for uninstall custom target
#

if(NOT EXISTS "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt")
	message(WARNING "Install manifest not found at @CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt, exiting from uninstall process.")
	return()
endif()

# Read manifest

file(READ "@CMAKE_CURRENT_BINARY_DIR@/install_manifest.txt" installed_files)

string(REGEX REPLACE "\n" ";" installed_files ${installed_files})

# Delete all files in manifest

foreach(file ${installed_files})

	message(STATUS "Uninstalling $ENV{DESTDIR}${file}")

	if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")

		# Delete the file
		exec_program(
			"@CMAKE_COMMAND@" ARGS "-E remove \"$ENV{DESTDIR}${file}\""
			OUTPUT_VARIABLE remove_output
			RETURN_VALUE remove_return
		)

		# Check if command was executed correctly
		if(NOT "${remove_return}" STREQUAL 0)

			message(WARNING "Invalid deletion of $ENV{DESTDIR}${file}.")

		endif()

	else()

		message(STATUS "File $ENV{DESTDIR}${file} not found.")

	endif()

endforeach()
